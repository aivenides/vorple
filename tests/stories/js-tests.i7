[The title tests Unicode characters in browser document title]
The story title is "Vorple Integration Tests - αβγ - абв".

Include Vorple by Juhana Leinonen.
Include Vorple Hyperlinks by Juhana Leinonen.

There is a room.


Book 1 - Verbs

Chapter 1 - Echoing

Echoing is an action out of world applying to a topic.
Understand "echo [text]" as echoing.

Carry out echoing:
	say the topic understood.


Chapter 2 - Setting a flag

Setting a flag is an action out of world applying to a topic.
Understand "set flag [text]" as setting a flag.

Carry out setting a flag:
	execute JavaScript command "window.testFlags[bracket]'[escaped topic understood]'[close bracket] = true".

First when play begins (this is the create the flag object rule):
	execute JavaScript command "window.testFlags = {}".


Chapter 3 - Setting a value

Setting a value is an action out of world applying to a topic.
Understand "set value [text]" as setting a value.

Carry out setting a value:
	execute JavaScript command "window.testValue='[escaped topic understood]'".


Chapter 4 - Pausing

Pausing is an action out of world applying to nothing.
Understand "pause" as pausing.

Carry out pausing:
	wait for keypress;
	place a block level element called "pause-over". [unit tests will detect that this element has appeared]

To wait for keypress:
	(- KeyPause(); -).
	
Include (-
	[ KeyPause key; 
		while ( 1 )
		{
			key = VM_KeyChar();
			if ( key == -4 or -5 or -10 or -11 or -12 or -13 )
			{
				continue;
			}
			rfalse;
		}
	];
-).


Chapter 5 - Forcefully quitting

[quit without prompting the player]
Forcefully quitting is an action out of world applying to nothing.
Understand "force quit" as forcefully quitting.

Carry out forcefully quitting:
	stop the game abruptly.

To stop the/-- game abruptly:
	(- quit; -).


[The link is used to verity that disabling the UI disables the links as well]
When play begins:
	say "Command hyperlink: ";
	place a link to the command "set flag link clicked" called "link-click".

[Copy a variable that should have been set at a custom init event, for checking that it's been set correctly]
When play begins:
	execute JavaScript command "window.initEventConfirmed = window.initEventHasRun".
