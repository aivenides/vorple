[The title tests Unicode characters in browser document title]
The story title is "Vorple Integration Tests - αβγ - абв".

Include Vorple by Juhana Leinonen.
Include Vorple Hyperlinks by Juhana Leinonen.

There is a room.

Setting a flag is an action out of world applying to a topic.

Understand "set flag [text]" as setting a flag.

Carry out setting a flag:
	execute JavaScript command "window.testFlags[bracket]'[escaped topic understood]'[close bracket] = true".

Pausing is an action out of world applying to nothing.
Understand "pause" as pausing.

Carry out pausing:
	wait for keypress;
	place a block level element called "pause-over". [unit tests will detect that this element has appeared]

To wait for keypress:
	(- KeyPause(); -).
	
Include (-
	[ KeyPause key; 
		while ( 1 )
		{
			key = VM_KeyChar();
			if ( key == -4 or -5 or -10 or -11 or -12 or -13 )
			{
				continue;
			}
			rfalse;
		}
	];
-).

First when play begins (this is the create the flag object rule):
	execute JavaScript command "window.testFlags = {}".

[The link is used to verity that disabling the UI disables the links as well]
When play begins:
	say "Command hyperlink: ";
	place a link to the command "set flag link clicked" called "link-click".